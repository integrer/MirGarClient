apply plugin: 'com.android.application'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    buildFeatures {
        dataBinding = true
    }

    defaultConfig {
        applicationId "org.mirgar.android.cryptoelection"
        minSdkVersion 26 // Required by 'com.exonum.client'
        targetSdkVersion 29
        versionCode 1
        versionName "0.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude 'META-INF/google/protobuf/type.proto'
    }

    applicationVariants.all { variant ->
        variant.getRuntimeConfiguration().exclude group: 'com.goterl.lazycode', module: 'lazysodium-java'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.29.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_ver"

    //region android
    implementation "androidx.appcompat:appcompat:$rootProject.androidx_ver.appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.androidx_ver.constraint_layout"
    implementation "androidx.core:core-ktx:$rootProject.androidx_ver.core"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.androidx_ver.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.androidx_ver.lifecycle"
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.androidx_ver.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.androidx_ver.navigation"
    implementation "androidx.viewpager:viewpager:$rootProject.androidx_ver.viewpager"
    implementation "com.google.android.material:material:$rootProject.android_ver.material"
    //endregion

    // kodein
    implementation "org.kodein.di:kodein-di-generic-jvm:$rootProject.kodein_version"
    implementation "org.kodein.di:kodein-di-framework-android-x:$rootProject.kodein_version"

    // exonum
    implementation 'com.exonum.client:exonum-light-client:0.6.0'

    implementation 'com.goterl.lazycode:lazysodium-android:4.1.1'
    implementation 'net.java.dev.jna:jna:5.5.0'

    implementation project(path: ':common')

    // tests
    testImplementation "junit:junit:$rootProject.junit_ver"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.androidx_ver.test.junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.androidx_ver.test.espresso"
}
